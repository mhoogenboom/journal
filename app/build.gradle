
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
    }
}
apply plugin: 'com.android.application'

dependencies {
    compile 'com.android.support:support-v4:21.0.+'
    compile 'com.android.support:appcompat-v7:21.0.+'
    compile 'com.android.support:recyclerview-v7:21.0.+'
    compile 'com.google.android.gms:play-services:6.1.+'
    compile 'com.jakewharton:butterknife:6.0.+'
    compile 'com.squareup.okhttp:okhttp:2.1.+'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.1.+'
    compile 'com.squareup.retrofit:retrofit:1.7.+'
    compile 'com.squareup.dagger:dagger:1.2.+'
    provided 'com.squareup.dagger:dagger-compiler:1.2.+'
	compile project(':dabbler')
	
    androidTestCompile('com.jakewharton.espresso:espresso:1.1-r3') {
        exclude group: 'com.squareup.dagger', module: 'dagger'
        exclude group: 'javax.inject', module: 'javax.inject'
    }

    androidTestCompile('com.jakewharton.espresso:espresso-support-v4:1.1-r3') {
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.squareup.dagger', module: 'dagger'
        exclude group: 'javax.inject', module: 'javax.inject'
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.0.2"

    defaultConfig {
        applicationId "com.robinfinch.journal.app"
        minSdkVersion 16
        targetSdkVersion 21
        versionCode = (rootProject.ext.versionMajor * 100 + rootProject.ext.versionMinor) * 100 + rootProject.ext.versionPatch
        versionName "$rootProject.ext.versionMajor.$rootProject.ext.versionMinor.$rootProject.ext.versionPatch"
        project.ext.archivesBaseName = "journal-" + versionName
        testInstrumentationRunner "com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        preDexLibraries = !rootProject.hasProperty('disablePreDex')
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    signingConfigs {
        robinfinch {
            storeFile new File(ROBIN_FINCH_STORE_FILE)
            storePassword ROBIN_FINCH_STORE_PASSWORD
            keyAlias ROBIN_FINCH_KEY_ALIAS
            keyPassword ROBIN_FINCH_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            runProguard false
        }
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.robinfinch
        }
    }
}

